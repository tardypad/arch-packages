pkgbase=termite
pkgname=(
  'termite'
  'termite-terminfo'
)
pkgver=14.90
pkgrel=1
pkgdesc="A simple VTE-based terminal"
url="https://github.com/thestinger/termite/"
arch=('x86_64')
license=('LGPL')

depends=(
  'gtk3'
  'vte3'
  'vte-common'
)
makedepends=(
  'git'
  'ncurses'
)

source=(
  "git+https://github.com/tardypad/termite#tag=v${pkgver}"
)

sha512sums=(
  'SKIP'
)

build() {
  cd "${pkgbase}"
  make
}

package_termite() {
  depends+=( 'termite-terminfo' )
  backup=( etc/xdg/termite/config )

  make -C "${pkgbase}" DESTDIR="${pkgdir}" PREFIX=/usr install
  rm -r "${pkgdir}/usr/share/terminfo"
}

package_termite-terminfo() {
  pkgdesc='Terminfo for Termite, a simple VTE-based terminal'
  depends=( 'ncurses' )

  mkdir -p "${pkgdir}/usr/share/terminfo"
  tic -x -o "${pkgdir}/usr/share/terminfo" "${pkgbase}/termite.terminfo"
}
