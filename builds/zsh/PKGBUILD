pkgname=zsh
pkgver=5.6.2
pkgrel=3
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
url='http://www.zsh.org/'
arch=('x86_64')
license=('custom')

install=zsh.install

depends=(
  'pcre'
  'libcap'
  'gdbm'
)
makedepends=(
  'pcre'
  'libcap'
  'gdbm'
)

source=(
  "https://www.zsh.org/pub/zsh-${pkgver}.tar.xz"
  'zprofile'
  'vi-motion-word-alphanum-chars-only.patch'
)

sha512sums=(
  'f0a49e41b55eb478692ab5471d7c9828956b7e96bc82944202b0ef1c49a889b21a0e7682aa5f59fd0054ebfd866c2244c8a622e7aa46c13038af5c226c48a3a2'
  'b287e00d8de4dc4cfb1c52bb2aef1d4b191de3512baad4c91dc81e78ddc3e5bb07297f43924b022ac44ff401a348d8a9fa366e19ddc8ea1ea72df311f5ed0034'
  '6a2da76a1a260c9f9c0052f1344189e007c4601fd12f19b7cb303d91ef2af4d687879a0d69f440010d27c4cc3705de18fea75cf1cd31d59830641732a1b6965f'
)

backup=(
  'etc/zsh/zprofile'
)

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Set correct keymap path
  sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

  # Fix usb.ids path
  sed -i 's#/usr/share/misc/usb.ids#/usr/share/hwdata/usb.ids#g' Completion/Linux/Command/_lsusb

  # Remove unneeded and conflicting completion scripts
  for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
    rm -rf Completion/$_fpath
    sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
  done
  rm Completion/Linux/Command/_{pkgtool,rpmbuild}

  patch -Np1 < "${srcdir}/vi-motion-word-alphanum-chars-only.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
    --docdir=/usr/share/doc/zsh \
    --htmldir=/usr/share/doc/zsh/html \
    --enable-etcdir=/etc/zsh \
    --enable-zshenv=/etc/zsh/zshenv \
    --enable-zlogin=/etc/zsh/zlogin \
    --enable-zlogout=/etc/zsh/zlogout \
    --enable-zprofile=/etc/zsh/zprofile \
    --enable-zshrc=/etc/zsh/zshrc \
    --enable-maildir-support \
    --with-term-lib='ncursesw' \
    --enable-multibyte \
    --enable-function-subdirs \
    --enable-fndir=/usr/share/zsh/functions \
    --enable-scriptdir=/usr/share/zsh/scripts \
    --with-tcsetpgrp \
    --enable-pcre \
    --enable-cap \
    --enable-zsh-secure-free
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  HOME="${srcdir}" make check
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
