pkgname=contentful-cli
pkgver=0.16.1
pkgrel=1
pkgdesc="Contentful CLI"
arch=("any")
url="https://github.com/contentful/contentful-cli"
license=("MIT")

depends=(
  'nodejs'
)
makedepends=(
  'npm'
  'jq'
)

source=(
  "https://registry.npmjs.org/${pkgname}/-/${pkgname}-${pkgver}.tgz"
)

sha512sums=(
  '058bdcfaeb6e525a8b3caf5cef1dfcc1b14127bd0dfd4426a35278e9754a7651e60c47ad3a9e3f267158849bf8e9b68dcb039aca1590f5bb148da1d60c647084'
)

package() {
  npm install -g --user root --cache "${srcdir}/npm-cache" --prefix "${pkgdir}/usr" "${srcdir}/${pkgname}-${pkgver}.tgz"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  find "${pkgdir}"/usr -type d -exec chmod 755 {} +

  # Remove references to $pkgdir
  find "${pkgdir}" -type f -name package.json -print0 | xargs -0 sed -i "/_where/d"

  # Remove references to $srcdir
  local tmppackage="$(mktemp)"
  local pkgjson="${pkgdir}/usr/lib/node_modules/${pkgname}/package.json"
  jq '.|=with_entries(select(.key|test("_.+")|not))' "${pkgjson}" > "${tmppackage}"
  mv "${tmppackage}" "${pkgjson}"
  chmod 644 "${pkgjson}"
}
