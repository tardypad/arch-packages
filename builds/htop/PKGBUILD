pkgname=htop
pkgver=2.2.0
pkgrel=1
pkgdesc="Interactive text-mode process viewer"
arch=('x86_64')
url="https://github.com/hishamhm/${pkgname}"
license=('GPL')

depends=(
  'ncurses'
  'libnl'
)
makedepends=(
  'python'
)
optdepends=(
  'lsof: list open files for running process'
  'strace: attach to running process'
)

options=(
  '!emptydirs'
  debug
)

source=(
  https://hisham.hm/${pkgname}/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz
  vim-keybindings.patch
)

sha512sums=(
  'ec1335bf0e3e0387e5e50acbc508d0effad19c4bc1ac312419dc97b82901f4819600d6f87a91668f39d429536d17304d4b14634426a06bec2ecd09df24adc62e'
  '53f62675e92fef6a518814a49a5663df8205700034ce7ec15d67cd38cc365ee52df4152d6f57fd4f9edb6004ebbb6a19325eebc6d2710d6f95ad1fc6accd40e7'
)

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 < "${srcdir}/vim-keybindings.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-cgroup \
      --enable-delayacct \
      --enable-openvz \
      --enable-unicode \
      --enable-vserver

  make
}

package() {
  make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
}
