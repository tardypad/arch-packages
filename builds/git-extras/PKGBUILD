pkgname=git-extras
pkgver=4.6.0
pkgrel=2
pkgdesc="GIT utilities -- repo summary, commit counting, repl, changelog population and more"
arch=('any')
url="https://github.com/tj/${pkgname}"
license=('MIT')

depends=(
  'bash'
  'git'
)

source=(
  "${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
)

sha512sums=(
  '59345668f29fef6d9e2d68c4602f7ce8ae59bda1fd2ffc7d624dc571f6167c77c347b6e7d0938a57e83d67a428dc9c21deda608714e9b907a8c48ca007631539'
)

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr SYSCONFDIR=/etc install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # move bash completion to standard folder
  rm -rf "$pkgdir/etc/bash_completion.d"
  install -Dm0644 "etc/bash_completion.sh" "$pkgdir/usr/share/bash-completion/completions/git-extras"

  install -Dm0644 "etc/git-extras-completion.zsh" "$pkgdir/usr/share/zsh/site-functions/git-extras-completion.zsh"
}
