pkgbase=dotfiles-git
pkgname=(
  'dotfiles-light-git'
  'dotfiles-dark-git'
)
pkgver=r1847.edf012e2
pkgrel=2
pkgdesc="My dotfiles"
arch=('any')
url="https://github.com/tardypad/dotfiles"
license=('custom')

conflicts=(
  'dotfiles-git'
)

provides=(
  'dotfiles-git'
)

replaces=(
  'dotfiles-git'
)

makedepends=(
  'curl'
  'git'
  'openssh'
  'scdoc'
)

depends=(
  'fontconfig'
  'sudo'
  'systemd'
  'vim'
)

install=dotfiles-git.install

# to be changed depending on the machine
_branch=master

# use personal remote containing all the branches
source=(
  "git+ssh://damien@tardypad.me:11235/~/damien.git/dotfiles#branch=${_branch}"
)

sha512sums=(
  'SKIP'
)

pkgver() {
  cd dotfiles
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_dotfiles-light-git() {
  cd dotfiles
  make DESTDIR="$pkgdir" THEME=light all

  adjust_permissions
}

package_dotfiles-dark-git() {
  cd dotfiles
  make DESTDIR="$pkgdir" THEME=dark all

  adjust_permissions
}

adjust_permissions() {
  chown -R damien:damien "$pkgdir/home/damien"
  chmod 711 "$pkgdir/home/damien"
  chmod 700 "$pkgdir/home/damien/.config"
}
