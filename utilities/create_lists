#!/usr/bin/env zsh

COMMAND=${0:t}

init_variables() {
  OUTPUT_FOLDER=.
}


usage() {
  cat << EOF
usage: ${COMMAND} [<options>]

Create lists of explicitely installed packages per repository
ignoring the base system packages

Options:
  -h,  --help                 show this message only
  -o,  --output     FOLDER    folder to store the lists
EOF
}


error() {
  [[ -z "$1" ]] || echo "${COMMAND}: $1" >&2
  echo "Try '${COMMAND} --help' for more information."
  exit 1
}


create_lists() {
  # ignore base packages
  local ignore_packages=$(
    pacman --query --quiet --groups base base-devel
  )

  # ignore system packages installed during installation
  ignore_packages+='
efibootmgr
grub
intel-ucode
iw
os-prober
r8168
wpa_supplicant'

  local explicit_packages=$(
    pacman --query --quiet --explicit
  )

  for repo in $( pacconf --repo-list ); do
    local repo_packages_file="${OUTPUT_FOLDER}/${repo}"

    local repo_packages=$(
      comm -12 \
        <( echo "${explicit_packages}" | sort ) \
        <( paclist "${repo}" | cut --field 1 --delimiter ' ' | sort )
    )

    local filtered_repo_packages=$(
      comm -23 \
        <( echo "${repo_packages}" | sort ) \
        <( echo "${ignore_packages}" | sort )
     )

    local final_repo_packages=
    if [[ ! -f "${repo_packages_file}" ]]; then
      final_repo_packages="${filtered_repo_packages}"
    else
      local tmp_file=$( mktemp )
      echo "${filtered_repo_packages}" > "${tmp_file}"
      final_repo_packages=$(
        merge_packages_files "${repo_packages_file}" "${tmp_file}"
      )
      rm "${tmp_file}"
    fi

    echo "${final_repo_packages}" > "${repo_packages_file}"
  done
}


merge_packages_files() {
  # old file contains the packages list with comments to keep
  local old_packages_file="$1"
  local new_packages_file="$2"

  local common_packages=$(
    comm -12 \
      <( cut --field 1 --delimiter ' ' "${old_packages_file}" ) \
      "${new_packages_file}"
  )

  local new_packages=$(
    comm -13 \
      <( cut --field 1 --delimiter ' ' "${old_packages_file}" ) \
      "${new_packages_file}"
  )

  {
    echo "${common_packages}" \
      | while read common_package; do
        grep --extended-regexp "^${common_package}( |$)" "${old_packages_file}"
        done;
    echo "${new_packages}";
  } \
    | grep --invert-match '^$' \
    | sort --key 1,1
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -h|--help)
        usage
        exit 0
        ;;
      -o|--output)
        [[ -n "$2" ]] || error 'Missing --output value'
        OUTPUT_FOLDER="$2"
        shift 2
        ;;
      *)
        error "Invalid option '$1'"
        ;;
    esac
  done
}


validate_options() {
  if [[ ! -d "${OUTPUT_FOLDER}" ]]; then
    mkdir --parents "${OUTPUT_FOLDER}"
    [[ $? -eq 0 ]] || error
  fi
}


init_variables
parse_options "$@"
validate_options


create_lists
